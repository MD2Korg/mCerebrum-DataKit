version = '1.0.7'
apply from: 'versioning.gradle'
buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def store_file = hasProperty('MD2K_RELEASE_STORE_FILE') ? MD2K_RELEASE_STORE_FILE : System.getenv('MD2K_RELEASE_STORE_FILE')
def store_password= hasProperty('MD2K_RELEASE_STORE_PASSWORD') ? MD2K_RELEASE_STORE_PASSWORD : System.getenv('MD2K_RELEASE_STORE_PASSWORD')
def key_alias = hasProperty('MD2K_RELEASE_KEY_ALIAS') ? MD2K_RELEASE_KEY_ALIAS : System.getenv('MD2K_RELEASE_KEY_ALIAS')
def key_password = hasProperty('MD2K_RELEASE_KEY_PASSWORD') ? MD2K_RELEASE_KEY_PASSWORD : System.getenv('MD2K_RELEASE_KEY_PASSWORD')

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'
    useLibrary 'org.apache.http.legacy' //For httpclient
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 21
        versionCode buildVersionCode()
        versionName version
    }


    signingConfigs {
        release {

            storeFile file(store_file)
            storePassword store_password
            keyAlias key_alias
            keyPassword key_password
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//            signingConfig getSigningConfig()
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    output.outputFile = new File(output.outputFile.parent, output.outputFile.name.replace("-release", "${variant.versionName}"))
                }
            }
        }
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':datakitapi')
    compile project(':utilities')
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.esotericsoftware:kryo:3.0.3'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.1@aar') {
        transitive = true;
    }
    compile 'org.apache.httpcomponents:httpclient-android:4.3.5'
    compile('org.apache.httpcomponents:httpmime:4.3.6') {
        exclude module: "httpclient-android"
    }
    apt 'com.bluelinelabs:logansquare-compiler:1.3.7'
    compile 'com.bluelinelabs:logansquare:1.3.7'
}
